plugins {
	id 'org.springframework.boot' version '2.2.2.RELEASE'
	id 'org.liquibase.gradle' version '2.0.1'
	id 'org.unbroken-dome.test-sets' version '2.2.1'
	id 'org.sonarqube' version '2.7'
	id 'jacoco'
	id 'java'
}

apply plugin: 'io.spring.dependency-management'

group = 'net.zevrant-services'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

ext {
	set('springCloudVersion', 'Hoxton.RELEASE')
}

repositories {
	mavenLocal()
	mavenCentral()
	maven {
		url "s3://zevrant-artifact-store"
		authentication {
			awsIm(AwsImAuthentication)
		}
	}
}
dependencies {
	implementation 'net.zevrant-services:zevrant-security-common:0.0.46'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	implementation group: 'org.springframework.security', name: 'spring-security-oauth2-client', version: '5.2.1.RELEASE'
	implementation group: 'org.springframework', name: 'spring-aop', version: '5.2.5.RELEASE'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.liquibase:liquibase-core:3.8.9'
	liquibaseRuntime 'org.liquibase:liquibase-core:3.8.9'
	liquibaseRuntime 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'io.jsonwebtoken:jjwt:0.9.1'
	implementation 'org.jboss.aerogear:aerogear-otp-java:1.0.0'
	liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:2.0.1'
	implementation 'org.springframework.cloud:spring-cloud-starter-oauth2'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	runtimeOnly 'org.postgresql:postgresql'
	liquibaseRuntime 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

jacoco {
	toolVersion = '0.8.5'
}

jacocoTestReport {
	reports {
		xml.enabled true
	}
}

liquibase {
	activities {
		main {
			changeLogFile 'src/main/resources/db/changelog/db.changelog-master.yaml'
			url 'jdbc:postgresql://localhost:5432/oauth2'
			username 'sa'
			password 'example'
			classpath 'src/main/resources'
		}
	}
	runList = 'main'
}

sonarqube {
	properties {
		property 'sonar.projectName', 'zevrant-oauth2-service'
		property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
		property "sonar.buildbreaker.skip" , "false"
		property "sonar.exclusions", "**/config/*,**/entity,**/ZevrantOauth2ServiceApplication.java,**/rest/**,**/exceptions/**"
	}
}

testSets {
	integrationTest { dirName = 'integrationTest' }
}

integrationTest {
	useJUnitPlatform ()
}

check.dependsOn integrationTest
integrationTest.dependsOn test
jacocoTestReport.dependsOn integrationTest
project.tasks["sonarqube"].dependsOn jacocoTestReport